Imports MailKit.Net.Imap ' For connecting to email via IMAP
Imports MailKit ' General MailKit library
Imports MimeKit ' For handling email messages
Imports MailKit.Search ' For searching and filtering emails

Module SpamFilter

    Sub Main()

        ' CONFIGURATION
        ' Never hardcode sensitive information into your code

        ' Replace with your email address
        Dim email = "your_email@gmail.com"

        ' Replace with your GMail app password, which can be generated by following this link:  https://myaccount.google.com/apppasswords 
        ' Make sure you have 2-Step Verification enabled on your Google account
        Dim appPassword = "your_password"

        ' Keywords to identify spam/undesireable emails, can be replaced with your own
        Dim spamKeywords() As String = {"win money", "win tickets", "lottery", "free gift", "urgent response", "click here"}

        ' Connection to Gmail IMAP server
        Using client As New ImapClient()
            Try
                client.Connect("imap.gmail.com", 993, True)
                client.Authenticate(email, appPassword)

                ' Opens the inbox folder
                Dim inbox = client.Inbox
                inbox.Open(FolderAccess.ReadWrite)

                ' Fetches the last 50 messages from the inbox folder
                For i As Integer = inbox.Count - 1 To Math.Max(0, inbox.Count - 50) Step -1

                    ' Fetches the message 
                    Dim message = inbox.GetMessage(i)

                    ' Converts subject to lowercase for keyword matching
                    Dim subject = If(message.Subject, "").ToLower()

                    ' Checks if the subject contains any of the spam keywords
                    For Each keyword In spamKeywords
                        If subject.Contains(keyword) Then

                            ' If the subject contains a spam keyword, logs the message
                            Console.WriteLine("Moving to spam: " & subject)

                            ' Moves the message to the spam folder
                            Dim spamFolder = client.GetFolder(SpecialFolder.Junk)
                            inbox.MoveTo(i, spamFolder)

                            ' Stops checking the rest of the message and moves on to the next
                            Exit For
                        End If
                    Next
                Next

                ' Disconnects from the server
                client.Disconnect(True)

            Catch ex As Exception

                'Handles any exceptions that occur during the connection or processing
                Console.WriteLine("Error: " & ex.Message)
            End Try
        End Using

        ' Waits for user to press a key to exit
        Console.WriteLine("Done. Press any key to exit.")
        Console.ReadKey(True)
    End Sub

End Module
